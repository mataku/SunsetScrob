apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'deploygate'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'io.fabric'
apply plugin: 'de.mannodermaus.android-junit5'
apply from: 'lint-checks.gradle'

android {
    compileSdkVersion buildConfig.compileSdk

    dataBinding {
        enabled = true
    }
    signingConfigs {
        release {
            storeFile file('../SunsetScrob.jks')
            storePassword System.getenv('SUNSET_STORE_PASSWORD')
            keyAlias System.getenv('SUNSET_KEY_ALIAS')
            keyPassword System.getenv('SUNSET_KEY_PASSWORD')
        }
        debug {
            storeFile file('../debug.keystore')
        }
    }
    defaultConfig {
        applicationId "com.mataku.scrobscrob"

        minSdkVersion buildConfig.targetSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 22
        versionName "0.2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField 'String', 'API_KEY', properties.API_KEY
        buildConfigField 'String', 'SHARED_SECRET', properties.SHARED_SECRET
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.dev'
        }
    }
    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        xmlReport false
        check lintchecks
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core')
    implementation project(':feature:licenses')

    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.cardview:cardview:$versions.androidx"
    implementation "androidx.browser:browser:$versions.androidx"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    implementation "com.google.android.material:material:$versions.androidx"
    implementation "androidx.preference:preference:$versions.androidx"

    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinCoroutines"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"

    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"
    implementation "jp.wasabeef:glide-transformations:$versions.glideTransformations"
    
    implementation "com.airbnb.android:epoxy:$versions.epoxy"
    implementation "com.airbnb.android:epoxy-databinding:$versions.epoxy"
    kapt "com.airbnb.android:epoxy-processor:$versions.epoxy"

    implementation "se.ansman.kotshi:api:$versions.kotshi"
    kapt "se.ansman.kotshi:compiler:$versions.kotshi"

    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$versions.retrofitKotlinCoroutinesAdapter"
    implementation "com.google.firebase:firebase-core:$versions.firebase"
    implementation "com.crashlytics.sdk.android:crashlytics:$versions.crashlytics"

    implementation "androidx.room:room-runtime:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"

    testImplementation "junit:junit:$versions.junit"
    testImplementation "com.google.guava:guava:$versions.guava"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$versions.kotlin"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"

    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.1"

    testImplementation "androidx.test:core:1.0.0"
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://maven.google.com'
    }
}

kapt {
    correctErrorTypes = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
