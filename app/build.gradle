apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'
apply plugin: 'deploygate'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-kapt'
apply from: 'lint-checks.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    dataBinding {
        enabled = true
    }
    signingConfigs {
        release {
            storeFile file('../SunsetScrob.jks')
            storePassword System.getenv('SUNSET_STORE_PASSWORD')
            keyAlias System.getenv('SUNSET_KEY_ALIAS')
            keyPassword System.getenv('SUNSET_KEY_PASSWORD')
        }
    }
    defaultConfig {
        applicationId "com.mataku.scrobscrob"

        minSdkVersion 24
        targetSdkVersion 26
        versionCode 5
        versionName "0.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField 'String', 'API_KEY', properties.API_KEY
        buildConfigField 'String', 'SHARED_SECRET', properties.SHARED_SECRET
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.dev'
        }
    }
    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        xmlReport false
        check lintchecks
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def support_library_version = "26.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    
    def retrofit2_version = "2.3.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    
    implementation 'com.google.code.gson:gson:2.8.0'
    
    def okhttp3_version = "3.4.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    
    def groupie_version = "2.0.3"
    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-kotlin-android-extensions:$groupie_version"
    implementation "com.xwray:groupie-databinding:$groupie_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'io.realm:android-adapters:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    kapt 'com.android.databinding:compiler:3.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.guava:guava:22.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"
}
repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com'
    }
}
